buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}
apply plugin: 'forge'
apply from: 'https://raw.githubusercontent.com/BuiltBrokenModding/BuiltBrokenScripts/buildScripts/scripts/both/jars.gradle'
apply from: 'https://raw.githubusercontent.com/BuiltBrokenModding/BuiltBrokenScripts/buildScripts/scripts/both/clean.gradle'

repositories {
    maven {
        name "K4Repo"
        url "http://maven.k-4u.nl"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    ivy {
		name = 'ThaumCraft 4 API'
		artifactPattern 'https://dl.dropboxusercontent.com/u/47135879/[module](-[classifier])-1.7.10-[revision](.[ext])'
	}
    maven {
        name "RX14"
        url "http://mvn.rx14.co.uk/repo/"
    }
    maven {
        name "Tterrag"
        url "http://maven.tterrag.com"
    }
}

project.ext {
    buildNum = System.getenv("BUILD_NUMBER")
    mcVersion = "1.7.10"
}

group= "brassgoggledcoders" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "steamcraft2"

minecraft {
    version = "${mcVersion}-10.13.4.1490-${mcVersion}"
    runDir = "eclipse"
}

version = "${project.minecraft.version}-B4.5."

if (System.getenv("BUILD_NUMBER") != null){
    version += project.ext.buildNum }
else
{
    version += getDate()
}
println "version = "+ version

dependencies {
    compile "brassgoggledcoders:boilerplate:1.7.10-6.0.0.+:deobf"
    compile 'com.mod-buildcraft:buildcraft:6.4.16:dev'
    compile 'net.industrial-craft:industrialcraft-2:2.2.727-experimental:api'
    compile 'Azanor:Thaumcraft:4.2.3.5:deobf@jar'
    compile 'team.chisel:Chisel:2.9.0.3:deobf'
    compile 'team.chisel.ctmlib:CTMLib:MC1.7.10-1.3.0.2:deobf'
    compile name: "Botania", group: "vazkii.botania", version: "r1.6-190.6", classifier: "deobf"
}

task getLibraries {
    def libsFolder = new File('libs')
    if (!libsFolder.isDirectory()) {
        libsFolder.mkdirs()
    }

    def railcraftSource = 'https://dl.dropboxusercontent.com/u/38558957/Minecraft/Railcraft/Railcraft_1.7.10-9.7.0.0-dev.jar'
    def railcraftDest = new File('libs', 'Railcraft_1.7.10-9.7.0.0-dev.jar')

    if(!railcraftDest.exists()){
        railcraftDest.withOutputStream { out ->
            new URL(railcraftSource).withInputStream { from ->  out << from; }
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		//include '**/*.lang'
		include '**/*.info'
		include '**/*.properties'

		expand 'version': project.version, 'mcversion': project.minecraft.version
		// replace version and MCVersion
	}
    
	// copy everything else, thats not text
	from(sourceSets.main.resources.srcDirs) {
		exclude '**/*.info'
		exclude '**/*.properties'
	}
}

jar {
    classifier = "universal"
}

// Create API library zip
task apiZip(type: Zip) {
    from(sourceSets.main.java) {
        include 'steamcraft/api/**'

        destinationDir = file 'output'

        classifier = "api"
    }
}

apiZip.mustRunAfter deobfJar

artifacts {
    archives deobfJar
}

task("uploadJars", dependsOn: "reobf") {
    description = "uploads JARs"
    if (System.getenv("local_maven") != null) {
        apply plugin: 'maven'
        uploadArchives {
            repositories {
                mavenDeployer {
                    repository(url: "file://" + System.getenv("local_maven"))
                    pom {
                        groupId = project.group
                        version = project.version
                        artifactId = project.archivesBaseName
                        project {
                            name project.archivesBaseName
                            packaging 'jar'
                            description 'SteamCraft2'
                            url ''
                            scm {
                                url 'https://github.com/BrassGoggledCoders/SteamCraft2'
                                connection 'scm:git:git@github.com:BrassGoggledCoders/SteamCraft2.git'
                                developerConnection 'scm:git:git@github.com:BrassGoggledCoders/SteamCraft2.git'
                            }
                            issueManagement {
                                system 'github'
                                url 'https://github.com/BrassGoggledCoders/SteamCraft2/issues'
                            }
                            licenses {
                                license {
                                    name 'MMPL'
                                    url 'https://github.com/BrassGoggledCoders/SteamCraft2/blob/master/LICENSE'
                                    distribution 'repo'
                                }
                            }
                            developers {
                                developer {
                                    id 'warlordjones'
                                    name 'warlordjones'
                                    roles { role 'developer' }
					//TODO
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
configurations.all {
    // check for updates every build
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}